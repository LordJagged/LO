(mod lib/std)

(global CHAR_TAB       u8 009)
(global CHAR_NEWLINE   u8 010)
(global CHAR_SPACE     u8 032)
(global CHAR_SEMICOLON u8 059)
(global CHAR_LPAREN u8 040) (global CHAR_RPAREN u8 041)
(global CHAR_LCURLY u8 123) (global CHAR_RCURLY u8 125)
(global CHAR_LBRACK u8 091) (global CHAR_RBRACK u8 093)

(global Parser/SExpr/Atom u8 0)
(global Parser/SExpr/List u8 1)
(struct Parser/SExpr
    (kind u8) ; Parser/SExpr/Atom | Parser/SExpr/List
    (data ptr) ; pointer to Atom's chars or List's expr vector
)

(global Parser/ParseError/UnexpectedEOF  u32 0)
(global Parser/ParseError/UnexpectedChar u32 1)
(struct Parser/ParseResult
    (ok bool)
    (index u32)
    (data u32) ; Parser/ParseError/* or pointer to some result
)

(fn Parser/parse [(chars StringSlice)] [Parser/ParseResult] (
    {index : u32}
    {index = 0}

    {index = (Parser/skip_space chars index)}

    {&items : &Vec}
    {&items = (Vec/new 6 (sizeof Parser/SExpr))}

    (loop (
        (if {index >= {chars . size}} (break))

        {char_res : Parser/CharAtResult}
        {char_res = (Parser/char_at chars index)}

        (if (not {char_res . ok})
            (return (struct.new Parser/ParseResult
                false index Parser/ParseError/UnexpectedEOF
            ))
        )

        (if (not (Parser/is_list_start {char_res . char}))
            (return (struct.new Parser/ParseResult
                false index Parser/ParseError/UnexpectedChar
            ))
        )

        {res : Parser/ParseResult}
        {res = (Parser/parse_list chars index)}
        (if (not {res . ok}) (return res))

        (Vec/push_all &items {res . data} 1)

        {index = (Parser/skip_space chars {res . index})}
    ))

    (return (struct.new Parser/ParseResult true index &items))
))

(fn Parser/parse_expr [(chars StringSlice) (index u32)] [Parser/ParseResult] (
    {char_res : Parser/CharAtResult}
    {char_res = (Parser/char_at chars index)}

    (if (not {char_res . ok})
        (return (struct.new Parser/ParseResult
            false index Parser/ParseError/UnexpectedEOF
        ))
    )

    (if (Parser/is_list_start {char_res . char})
        (return (Parser/parse_list chars index))
    )

    (return (Parser/parse_atom chars index))
))

(fn Parser/parse_atom [(chars StringSlice) (index u32)] [Parser/ParseResult] (
    {atom_len : u32}
    {atom_len = 0}

    (loop (
        {char_res : Parser/CharAtResult}
        {char_res = (Parser/char_at chars index)}
        (if (not {char_res . ok}) (break))
        (if (|| (||
            (Parser/is_space {char_res . char})
            (Parser/is_list_end {char_res . char}))
            {{char_res . char} == CHAR_SEMICOLON})
            (break)
        )
        {index = {index + 1}}
        {atom_len = {atom_len + 1}}
    ))

    {&atom : &Vec}
    {&atom = (Vec/new atom_len 1)} ; TODO: replace 1 with (sizeof u8)
    (Vec/push_all &atom
        {{{chars . data} + index} - atom_len}
        atom_len
    )

    {&expr : ptr}
    {&expr = (Mem/alloc (sizeof Parser/SExpr))}
    (store Parser/SExpr &expr (struct.new Parser/SExpr
        Parser/SExpr/Atom
        &atom
    ))

    (return (struct.new Parser/ParseResult true index &expr))
))

(fn Parser/parse_list [(chars StringSlice) (index u32)] [Parser/ParseResult] (
    {char_res : Parser/CharAtResult}
    {char_res = (Parser/char_at chars index)}

    {list_start_char : u8}
    {list_start_char = {char_res . char}}
    {index = {index + 1}} ; eat list start

    {index = (Parser/skip_space chars index)}

    {&items : &Vec}
    {&items = (Vec/new 6 (sizeof Parser/SExpr))}

    (loop (
        {char_res = (Parser/char_at chars index)}
        (if (not {char_res . ok})
            (return (struct.new Parser/ParseResult
                false index Parser/ParseError/UnexpectedEOF
            ))
        )

        (if (Parser/is_list_end {char_res . char})
            (break)
        )

        {res : Parser/ParseResult}
        {res = (Parser/parse_expr chars index)}
        (if (not {res . ok}) (return res))

        (Vec/push_all &items {res . data} 1)

        {index = (Parser/skip_space chars {res . index})}
    ))

    {char_res = (Parser/char_at chars index)}
    (if (not {char_res . ok})
        (return (struct.new Parser/ParseResult
            false index Parser/ParseError/UnexpectedEOF
        ))
    )

    {list_end_char : u8}
    {list_end_char = {char_res . char}}
    {index = {index + 1}} ; eat list end

    (if (not (Parser/is_valid_list_chars list_start_char list_end_char))
        (return (struct.new Parser/ParseResult
            false index Parser/ParseError/UnexpectedEOF
        ))
    )

    (if {{list_start_char == CHAR_LCURLY} && {(Vec/len &items) >= 2}}
        (Vec/swap &items 0 1)
    )

    {&expr : ptr}
    {&expr = (Mem/alloc (sizeof Parser/SExpr))}
    (store Parser/SExpr &expr (struct.new Parser/SExpr
        Parser/SExpr/List
        &items
    ))

    (return (struct.new Parser/ParseResult true index &expr))
))

(fn Parser/skip_space [(chars StringSlice) (index u32)] [u32] (
    {char_res : Parser/CharAtResult}

    (loop (
        {char_res = (Parser/char_at chars index)}
        (if (not {char_res . ok}) (break))
        (if (not (Parser/is_space {char_res . char})) (break))
        {index = {index + 1}}
    ))

    (if {{char_res . ok} && {{char_res . char} == CHAR_SEMICOLON}} (do
        {index = {index + 1}}

        (loop (
            {char_res = (Parser/char_at chars index)}
            (if (not {char_res . ok}) (break))
            (if {{char_res . char} == CHAR_NEWLINE} (break))
            {index = {index + 1}}
        ))

        (if {{char_res . ok} && {{char_res . char} == CHAR_SEMICOLON}}
            {index = {index + 1}}
        )

        (return (Parser/skip_space chars index))
    ))

    (return index)
))

(fn Parser/is_list_start [(char u8)] [bool] (
    (return (|| (||
        {char == CHAR_LPAREN}
        {char == CHAR_LCURLY})
        {char == CHAR_LBRACK}))
))

(fn Parser/is_list_end [(char u8)] [bool] (
    (return (|| (||
        {char == CHAR_RPAREN}
        {char == CHAR_RCURLY})
        {char == CHAR_RBRACK}))
))

(fn Parser/is_valid_list_chars [(start u8) (end u8)] [bool] (
    (return (|| (||
        {{start == CHAR_LPAREN} && {end == CHAR_RPAREN}}
        {{start == CHAR_LCURLY} && {end == CHAR_RCURLY}})
        {{start == CHAR_LBRACK} && {end == CHAR_RBRACK}}))
))

(fn Parser/is_space [(char u8)] [bool] (
    (return (|| (||
        {char == CHAR_SPACE}
        {char == CHAR_NEWLINE})
        {char == CHAR_TAB}))
))

(struct Position (line u32) (col u32))

(fn Parser/index_to_position [(chars StringSlice) (index u32)] [Position] (
    {pos : Position}
    {pos = (do 1 1)}

    {char_index : u32}
    {char_index = 0}
    (loop (
        (if {char_index >= {chars . size}} (break))
        (if {char_index >= index} (break))

        {char : u32}
        {char = (StringSlice/char_at chars char_index)}

        (if {char == CHAR_NEWLINE} (do
            {pos = (do {{pos . line} + 1} 1)}
            {char_index = {char_index + 1}}
            (continue)
        ))

        (set pos col {{pos . col} + 1})

        ;; TODO(assign-v2): impl this syntax:
        ;; {{pos . col} = {{pos . col} + 1}}

        {char_index = {char_index + 1}}
    ))

    (return pos)
))

(struct Parser/CharAtResult (ok bool) (char u8))

(fn Parser/char_at [(chars StringSlice) (index u32)] [Parser/CharAtResult] (
    (if {index >= {chars . size}}
        (return (struct.new Parser/CharAtResult false 0))
    )

    (return (struct.new Parser/CharAtResult
        true (StringSlice/char_at chars index)
    ))
))
