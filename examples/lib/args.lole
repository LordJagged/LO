(struct Args
    (argc u32)
    (&argv (&* (&* u8)))
    (&argv_buf (&* u8))
)

(fn Args/load [] [Args] (
    {&argc : (& u32) = (Stack/alloc (sizeof u32))}
    (defer (Stack/free (sizeof u32)))
    {&argv_buf_size : (& u32) = (Stack/alloc (sizeof u32))}
    (defer (Stack/free (sizeof u32)))
    (drop (Wasi/args_sizes_get &argc &argv_buf_size))

    {argv_buf_size : u32 = {u32 @ &argv_buf_size}}
    {argc : u32 = {u32 @ &argc}}

    {&argv : (&* (&* u8)) = (Heap/alloc {argc * (sizeof (&* u8))})}
    {&argv_buf : (&* u8) = (Heap/alloc argv_buf_size)}
    (drop (Wasi/args_get &argv &argv_buf))

    (return (struct.new Args argc &argv &argv_buf))
))

(fn Args/free [(self Args)] [] (
    (Heap/free {self . &argv})
    (Heap/free {self . &argv_buf})
))

(fn Args/get [(self Args) (i u32)] [StringSlice] (
    {&arg : (&* u8) = {ptr @ {self . &argv} @ {i * (sizeof (&* u8))}}}
    (return (std/c_str_to_slice &arg))
))
