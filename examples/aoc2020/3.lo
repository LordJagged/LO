include "lib/cli";
include "lib/fs";

type Tile = u8;
type Vec_Tile = Vec;
type Vec_Vec_Tile = Vec;

const TREE = 1 as Tile;
const EMPTY = 0 as Tile;
const SLOPE_RIGHT = 3;
const SLOPE_DOWN = 1;

fn main() -> u32 {
    let input = fs_read_file("./aoc2020/3.txt");
    defer Vec_free(input);

    let map = Map_parse(input);
    defer Map_free(map);

    let tree_count = 0;
    let i = 0;
    let j = 0;

    loop {
        i += SLOPE_DOWN;
        j += SLOPE_RIGHT;

        if i == map->size {
            break;
        };

        if Map_get(map, i, j) == TREE {
            tree_count += 1;
        };
    };

    print_u32(tree_count);
    puts("\n");

    return 0;
};

fn Map_parse(input: &String) -> &Vec_Vec_Tile {
    let map = Vec_new(4, sizeof &Vec_Tile);
    let row = Vec_new(4, sizeof Tile);

    let i = 0;
    loop {
        if i == input->size {
            break;
        };

        let char = *(Vec_at(input, i) as &u8);
        if char == char_code "#" as u8 {
            *(Vec_push(row) as &Tile) = TREE;
        };
        if char == char_code "." as u8 {
            *(Vec_push(row) as &Tile) = EMPTY;
        };
        if char == char_code "\n" as u8 {
            *(Vec_push(map) as & &Vec_Tile) = row;
            row = Vec_new(4, sizeof Tile);
        };

        i += 1;
    };

    return map;
};

fn Map_get(map: &Vec_Vec_Tile, i: u32, j: u32) -> Tile {
    let row = *(Vec_at(map, i) as & &Vec_Tile);
    return *(Vec_at(row, j % row->size) as &Tile);
};

fn Map_free(map: &Vec_Vec_Tile) {
    let i = 0;
    loop {
        if i == map->size {
            break;
        };

        let row = Vec_at(map, i) as & &Vec_Tile;
        Vec_free(row);

        i += 1;
    };

    Vec_free(map);
};
