include "lib/cli";
include "lib/fs";

type Tile = bool;
type Vec::Tile = Vec;
type Vec::Vec::Tile = Vec;

const TREE = 1 as Tile;
const EMPTY = 0 as Tile;

fn main() -> u32 {
    let input = fs_read_file("./aoc2020/3.txt");
    defer Vec::free(input);

    let map = Map_parse(input);
    defer Map_free(map);

    let result = 1u64
        * count_trees_on_slope(map, 1, 1) as u64
        * count_trees_on_slope(map, 1, 3) as u64
        * count_trees_on_slope(map, 1, 5) as u64
        * count_trees_on_slope(map, 1, 7) as u64
        * count_trees_on_slope(map, 2, 1) as u64;

    print_u64(result);
    puts("\n");

    return 0;
};

fn count_trees_on_slope(map: &Vec::Vec::Tile, slope_i: u32, slope_j: u32) -> u32 {
    let tree_count = 0;
    let i = 0;
    let j = 0;

    loop {
        i += slope_i;
        j += slope_j;

        if i >= map->size {
            break;
        };

        if Map_get(map, i, j) == TREE {
            tree_count += 1;
        };
    };

    return tree_count;
};

fn Map_parse(input: &String) -> &Vec::Vec::Tile {
    let map = Vec::new(4, sizeof &Vec::Tile);
    let row = Vec::new(4, sizeof Tile);

    let i = 0;
    loop {
        if i == input->size {
            break;
        };

        let char = *(Vec::at(input, i) as &u8);
        if char == char_code "#" as u8 {
            *(Vec::push(row) as &Tile) = TREE;
        };
        if char == char_code "." as u8 {
            *(Vec::push(row) as &Tile) = EMPTY;
        };
        if char == char_code "\n" as u8 {
            *(Vec::push(map) as & &Vec::Tile) = row;
            row = Vec::new(4, sizeof Tile);
        };

        i += 1;
    };

    return map;
};

fn Map_get(map: &Vec::Vec::Tile, i: u32, j: u32) -> Tile {
    let row = *(Vec::at(map, i) as & &Vec::Tile);
    return *(Vec::at(row, j % row->size) as &Tile);
};

fn Map_free(map: &Vec::Vec::Tile) {
    let i = 0;
    loop {
        if i == map->size {
            break;
        };

        let row = Vec::at(map, i) as & &Vec::Tile;
        Vec::free(row);

        i += 1;
    };

    Vec::free(map);
};
