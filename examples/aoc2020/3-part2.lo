include "lib/cli";
include "lib/fs";

type Tile = u8;
type Vec_Tile = Vec;
type Vec_Vec_Tile = Vec;

const TREE = 1 as Tile;
const EMPTY = 0 as Tile;

fn main() -> u32 {
    let input = fs_read_file("./aoc2020/3.txt");
    defer Vec_free(input);

    let map = Map_parse(input);
    defer Map_free(map);

    let result = u64 1
        * count_trees_on_slope(map, 1, 1) as u64
        * count_trees_on_slope(map, 1, 3) as u64
        * count_trees_on_slope(map, 1, 5) as u64
        * count_trees_on_slope(map, 1, 7) as u64
        * count_trees_on_slope(map, 2, 1) as u64;

    print_u64(result);
    puts("\n");

    return 0;
};

fn count_trees_on_slope(map: &Vec_Vec_Tile, slope_i: u32, slope_j: u32) -> u32 {
    let tree_count = 0;
    let i = 0;
    let j = 0;

    loop {
        i += slope_i;
        j += slope_j;

        if i >= map->size {
            break;
        };

        if Map_get(map, i, j) == TREE {
            tree_count += 1;
        };
    };

    return tree_count;
};

fn Map_parse(input: &String) -> &Vec_Vec_Tile {
    let map = Vec_new(4, sizeof &Vec_Tile);
    let row = Vec_new(4, sizeof Tile);

    let i = 0;
    loop {
        if i == input->size {
            break;
        };

        let char = *(Vec_at(input, i) as &u8);
        if char == char_code "#" as u8 {
            *(Vec_push(row) as &Tile) = TREE;
        };
        if char == char_code "." as u8 {
            *(Vec_push(row) as &Tile) = EMPTY;
        };
        if char == char_code "\n" as u8 {
            *(Vec_push(map) as & &Vec_Tile) = row;
            row = Vec_new(4, sizeof Tile);
        };

        i += 1;
    };

    return map;
};

fn Map_get(map: &Vec_Vec_Tile, i: u32, j: u32) -> Tile {
    let row = *(Vec_at(map, i) as & &Vec_Tile);
    return *(Vec_at(row, j % row->size) as &Tile);
};

fn Map_free(map: &Vec_Vec_Tile) {
    let i = 0;
    loop {
        if i == map->size {
            break;
        };

        let row = Vec_at(map, i) as & &Vec_Tile;
        Vec_free(row);

        i += 1;
    };

    Vec_free(map);
};
