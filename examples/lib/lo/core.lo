include "../std.lo";
include "../fs.lo";

include "./wasm.lo";

struct LoError {
    message: &String,
};

type Vec::LoIncludedFile = Vec;

struct LoIncludedFile {
    path: &String,
    contents: &String,
};

struct LoContext {
    included_files: &Vec::LoIncludedFile,
    wasm_module: &WasmModule,
};

fn LoContext::new(): &LoContext {
    return heap::new!<LoContext>(LoContext {
        included_files: Vec::new!<LoIncludedFile>(),
        wasm_module: heap::new!<WasmModule>(WasmModule {}),
    });
};

fn LoContext::include_file(&self, file_path: str): u32 throws &LoError {
    self.included_files.push!<LoIncludedFile>(LoIncludedFile {
        path: String::from_str(file_path),
        contents: fs::read_file(file_path) catch err {
            let message = String::from_str("Cannot include file: ");
            message.push_all(file_path.data as &void, file_path.size);
            message.push!<u8>('\n' as u8);

            throw heap::new!<LoError>(LoError {
                message: message,
            });
        },
    });

    return self.included_files.size - 1;
};

fn LoContext::get_file(&self, file_index: u32): &LoIncludedFile {
    return self.included_files.at!<LoIncludedFile>(file_index);
};
