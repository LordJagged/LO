include "./utils.lo";
include "../print.lo";

type LoTokenType = u8;
const LoTokenType::StringLiteral = 0 as u8;
const LoTokenType::CharLiteral   = 1 as u8;
const LoTokenType::IntLiteral    = 2 as u8;
const LoTokenType::Symbol        = 3 as u8;
const LoTokenType::Delim         = 4 as u8;
const LoTokenType::Operator      = 5 as u8;

type Vec::LoToken = Vec;
struct LoToken {
    type_: LoTokenType,
    repr: str,
    value: &void,
    loc: LoLocation,
};

struct LoTokenStream {
    tokens: &Vec::LoToken,
    index: u32,
    terminal_token: LoToken,
};

fn LoTokenStream::new(tokens: &Vec::LoToken, eof_location: LoLocation): &LoTokenStream {
    return heap::new!<LoTokenStream>(LoTokenStream {
        tokens: tokens,
        index: 0,
        terminal_token: LoToken {
            type_: LoTokenType::Symbol,
            repr: "<EOF>",
            value: 0 as &void,
            loc: eof_location,
        },
    });
};

struct LoLexer {
    file_index: u32,
    chars: str,
    index: u32,
    line: u32,
    col: u32,
    was_newline: bool,
};

fn LoLexer::new(file_index: u32, chars: str): &LoLexer {
    return heap::new!<LoLexer>(LoLexer {
        file_index: file_index,
        chars: chars,
        index: 0,
        line: 1,
        col: 1,
        was_newline: false,
    });
};

fn LoLexer::lex_all(&self): &LoTokenStream throws &LoError {
    let tokens = Vec::new!<&LoToken>();

    self.skip_space();

    // TODO: support while?
    loop {
        if self.index >= self.chars.size {
            break;
        };

        // TODO: tokens could be a Vec of structs instead of references
        //   but there is a bug with `<struct> throws *` types
        tokens.push!<&LoToken>(self.lex_token() catch err {
            throw err;
        });

        self.skip_space();
    };

    return LoTokenStream::new(tokens, self.loc());
};

type LexResult = &LoToken throws &LoError;

fn LoLexer::lex_token(&self): LexResult {
    let char = self.current_char() catch err {
        throw err;
    };

    if LoLexer::is_symbol_char(char) {
        return self.lex_symbol() catch err {
            throw err;
        };
    };

    // TODO: lex other tokens

    let message = String::from_str("Unexpected char: ");
    message.push!<u8>(char);

    throw heap::new!<LoError>(LoError {
        message: message,
        loc: self.loc(),
    });
};

fn LoLexer::lex_symbol(&self): LexResult {
    let loc = self.loc();

    loop {
        let char = self.current_char() catch err {
            throw err;
        };

        if !LoLexer::is_symbol_char(char) {
            break;
        };

        self.next_char();
    };

    loc.end_pos = self.pos();

    return heap::new!<LoToken>(LoToken {
        type_: LoTokenType::Symbol,
        repr: self.chars.slice(loc.pos.offset, self.index),
        value: 0 as &void,
        loc: loc,
    });
};

fn LoLexer::skip_space(&self) {
    let char = '\0' as u8;
    loop {
        char = self.current_char() catch _ {
            return;
        };

        if !LoLexer::is_space_char(char) {
            break;
        };

        self.next_char();
    };

    // skip comment
    if char == '/' as u8 {
        let next = self.peek_next_char() catch _ {
            return;
        };

        if next != '/' as u8 {
            return;
        };

        loop {
            self.next_char();

            char = self.current_char() catch _ {
                return;
            };

            if char == '\n' as u8 {
                self.skip_space();
                break;
            };
        };
    };
};

fn LoLexer::next_char(&self) {
    self.index += 1;

    let char = self.current_char() catch _ {
        return;
    };

    self.col += 1;

    if char == '\n' as u8 {
        if self.was_newline {
            self.line += 1;
        };
        self.was_newline = true;
        return;
    };

    if self.was_newline {
        self.line += 1;
        self.col = 1;
        self.was_newline = false;
        return;
    };
};

fn LoLexer::current_char(&self): u8 throws &LoError {
    if self.index >= self.chars.size {
        throw self.err_unexpected_eof();
    };

    return self.chars.char_at(self.index);
};

fn LoLexer::peek_next_char(&self): u8 throws &LoError {
    if self.index + 1 >= self.chars.size {
        throw self.err_unexpected_eof();
    };

    return self.chars.char_at(self.index + 1);
};

fn LoLexer::err_unexpected_eof(&self): &LoError {
    return heap::new!<LoError>(LoError {
        message: String::from_str("ParseError: Unexpected EOF"),
        loc: self.loc(),
    });
};

fn LoLexer::loc(&self): LoLocation {
    return LoLocation {
        file_index: self.file_index,
        pos: self.pos(),
        end_pos: self.pos(),
    };
};

fn LoLexer::pos(&self): LoPosition {
    return LoPosition {
        offset: self.index,
        line: self.line,
        col: self.col,
    };
};

fn LoLexer::is_space_char(char: u8): bool {
    return char == ' '  as u8
        || char == '\n' as u8
        || char == '\t' as u8
        || char == '\r' as u8;
};

fn LoLexer::is_symbol_char(char: u8): bool {
    return (char >= 'a' as u8 && char <= 'z' as u8)
        || (char >= 'A' as u8 && char <= 'Z' as u8)
        || (char >= '0' as u8 && char <= '9' as u8)
        || char == '_' as u8;
};
