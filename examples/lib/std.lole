(global true bool 1)
(global false bool 0)

; unrecoverable-errors

    (global Unrec/UNKNOWN u32 0)
    (global Unrec/STACK_OVERFLOW u32 1)
    (global Unrec/STACK_UNDERFLOW u32 2)
    (global Unrec/OOM u32 3)
    (global mut Unrec/ERR u32 0)

; alloc

    (global Alloc/STACK u32 0)
    (global Alloc/HEAP u32 1)

    (fn alloc [(alloc_id u32) (size u32)] [ptr] (
        (if {alloc_id == 0} (return (Stack/alloc size)))
        (return (Heap/alloc size))
    ))

    (fn free [(alloc_id u32) (ptr u32) (size u32)] [] (
        (if {alloc_id == 0} (return (Stack/free size)))
        (return (Heap/free ptr))
    ))

; stack

    (global mut Stack/&CURRENT u32 (data.size))
    (global mut Stack/&BEGIN ptr (data.size))
    (global mut Stack/&END u32 1048576)

    (fn Stack/push_u32 [(value u32)] [] (
        {i32 @ (Stack/alloc (sizeof u32)) = value}
    ))

    (fn Stack/read_u32 [] [u32] (
        (return {i32 @ {Stack/&CURRENT - (sizeof u32)}})
    ))

    (fn Stack/pop_u32 [] [u32] (
        (Stack/free (sizeof u32))

        (return {i32 @ Stack/&CURRENT})
    ))

    (fn Stack/alloc [(size u32)] [ptr] (
        {Stack/&CURRENT += size}

        (if {Stack/&CURRENT > Stack/&END} (do
            {Unrec/ERR = Unrec/STACK_OVERFLOW}
            (unreachable)
        ))

        (return {Stack/&CURRENT - size})
    ))

    (fn Stack/free [(size u32)] [] (
        {Stack/&CURRENT -= size}

        (if {Stack/&CURRENT < Stack/&BEGIN} (do
            {Unrec/ERR = Unrec/STACK_UNDERFLOW}
            (unreachable)
        ))
    ))

; heap

    (struct Heap/Block
        (size u32)
        (used bool)
        (&next ptr) ; (& Heap/Block)
        (&data ptr)
    )

    (global mut Heap/TOTAL_ALLOCATED u32 1048576) ; starts after stack end
    (global mut Heap/&FIRST_BLOCK (& Heap/Block) 0)
    (global mut Heap/&CURRENT_BLOCK (& Heap/Block) 0)

    ; http://dmitrysoshnikov.com/compilers/writing-a-memory-allocator/#mutator-allocator-collector
    (fn Heap/alloc [(size u32)] [ptr] (
        {size = (Mem/align size)}

        {&free_block : (& Heap/Block) = (Heap/find_free_block size)}
        (if {&free_block != 0} (do
            {Heap/Block @ &free_block . used = true}
            (return {Heap/Block @ &free_block . &data})
        ))

        {total_size_to_alloc : u32 = {size + (sizeof Heap/Block)}}

        {&block : (& Heap/Block) = Heap/TOTAL_ALLOCATED}
        {Heap/TOTAL_ALLOCATED += total_size_to_alloc}

        ; memory.grow enough if out of space
        (if {Heap/TOTAL_ALLOCATED >= {(memory.size) * Mem/PAGE_SIZE}} (do
            {pages_to_grow : u32 = (Mem/align_to total_size_to_alloc Mem/PAGE_SIZE)}
            {grow_res : u32 = (memory.grow pages_to_grow)}
            (if {grow_res == {0 - 1}} (do
                {Unrec/ERR = Unrec/OOM}
                (unreachable)
            ))
        ))

        {Heap/Block @ &block . size = size}
        {Heap/Block @ &block . used = true}
        {Heap/Block @ &block . &data = {&block + (sizeof Heap/Block)}}

        ; init heap
        (if {Heap/&FIRST_BLOCK == 0} (do
            {Heap/&FIRST_BLOCK = &block}
        ))

        ; chain block
        (if {Heap/&CURRENT_BLOCK != 0} (do
            {Heap/Block @ Heap/&CURRENT_BLOCK . &next = &block}
        ))

        {Heap/&CURRENT_BLOCK = &block}

        (return {Heap/Block @ &block . &data})
    ))

    (fn Heap/free [(&ref ptr)] [] (
        {&block : (& Heap/Block) = (Heap/get_block &ref)}
        {Heap/Block @ &block . used = false}
    ))

    (fn Heap/get_block [(&ref ptr)] [(& Heap/Block)] (
        (return {&ref - (sizeof Heap/Block)})
    ))

    ; first fit
    (fn Heap/find_free_block [(size u32)] [(& Heap/Block)] (
        {&block : (& Heap/Block) = Heap/&FIRST_BLOCK}

        (loop (
            (if {&block == 0} (break))

            (if {(not {Heap/Block @ &block . used})
                    && {Heap/Block @ &block . size >= size}} (do
                (return &block)
            ))

            {&block = {Heap/Block @ &block . &next}}
        ))

        (return 0)
    ))

; memory instructions

    (global Mem/POINTER_SIZE u32 4)
    (global Mem/PAGE_SIZE u32 65536)

    (fn Mem/align [(size u32)] [u32] (
        (return (Mem/align_to size Mem/POINTER_SIZE))
    ))

    (fn Mem/align_to [(size u32) (size_multiple u32)] [u32] (
        {remaining_to_align : u32 = {size % size_multiple}}
        (if {remaining_to_align == 0} (do
            (return size)
        ))

        {padding : u32 = {size_multiple - remaining_to_align}}
        (return {size + padding})
    ))

    ; TODO: use builtin mem.copy instr, check engine support
    (fn Mem/copy [(src ptr) (dist ptr) (size u32)] [] (
        (loop (
            (if {size == 0} (break))
            {size -= 1}

            {i32/u8 @ dist @ size = {i32/u8 @ src @ size}}
        ))
    ))

    (fn Mem/swap [(ref1 ptr) (ref2 ptr) (size u32)] [] (
        (loop (
            (if {size == 0} (break))
            {size -= 1}

            {
            (do {i32/u8 @ ref1 @ size}
                {i32/u8 @ ref2 @ size})
                =
            (do {i32/u8 @ ref2 @ size}
                {i32/u8 @ ref1 @ size})
            }
        ))
    ))

; vec

    (struct Vec
        (&data ptr)
        (length u32)
        (capacity u32)
        (item_size u32)
        (alloc_id u32)
    )

    (struct Vec<u8> (actual Vec))

    (fn Vec/new [(capacity u32) (item_size u32)] [(& Vec)] (
        (return (Vec/new_using capacity item_size Alloc/HEAP))
    ))

    (fn Vec/new_using [(capacity u32) (item_size u32) (alloc_id u32)] [(& Vec)] (
        (return (new Vec (do
            (alloc alloc_id {capacity * item_size})
            0
            capacity
            item_size
            alloc_id
        ) :using alloc_id))
    ))

    (fn Vec/free [(&self (& Vec))] [] (
        (free {Vec @ &self . alloc_id} &self (sizeof Vec))
        (free {Vec @ &self . alloc_id} {Vec @ &self . &data} {Vec @ &self . capacity})
    ))

    ; TODO: check out of bounds
    (fn Vec/get_u8 [(&self (& Vec)) (index u32)] [u8] (
        (return {i32/u8
            @ {Vec @ &self . &data}
            @ {index * {Vec @ &self . item_size}}})
    ))

    ; TODO: check out of bounds
    (fn Vec/swap [(&self (& Vec)) (index1 u32) (index2 u32)] [] (
        {item_size : u32 = {Vec @ &self . item_size}}
        (Mem/swap
            {Vec @ &self . &data + {index1 * item_size}}
            {Vec @ &self . &data + {index2 * item_size}}
            item_size
        )
    ))

    (fn Vec/len [(&self (& Vec))] [u32] (
        (return {Vec @ &self . length})
    ))

    (fn Vec/push_u8 [(&self (& Vec)) (item u8)] [] (
        {index : u32 = {Vec @ &self . length}}
        {Vec @ &self . length = {index + 1}}

        (Vec/grow_if_needed &self)

        {i32/u8
            @ {Vec @ &self . &data }
            @ {index * {Vec @ &self . item_size}} = item}
    ))

    (fn Vec/push_all [(&self (& Vec)) (&items ptr) (items_len u32)] [] (
        {old_len : u32 = {Vec @ &self . length}}
        {Vec @ &self . length = {old_len + items_len}}

        (Vec/grow_if_needed &self)

        (Mem/copy
            &items
            {Vec @ &self . &data + {old_len * {Vec @ &self . item_size}}}
            {items_len * {Vec @ &self . item_size}}
        )
    ))

    (fn Vec/push_undef [(&self (& Vec))] [ptr] (
        {Vec @ &self . length += 1}

        (Vec/grow_if_needed &self)

        (return (Vec/at &self {Vec @ &self . length - 1}))
    ))

    (fn Vec/pop_unchecked [(&self (& Vec))] [ptr] (
        {Vec @ &self . length -= 1}
        (return (Vec/at &self (Vec/len &self)))
    ))

    (fn Vec/grow_if_needed [(&self (& Vec))] [] (
        (if {Vec @ &self . length != {Vec @ &self . capacity}} (do
            (return)
        ))

        {item_size : u32 = {Vec @ &self . item_size}}
        {old_capacity : u32 = {Vec @ &self . capacity}}
        {&old_data : ptr = {Vec @ &self . &data}}
        (defer (free Alloc/HEAP &old_data old_capacity))

        {Vec @ &self . capacity = {old_capacity * 2}}
        {Vec @ &self . &data = (alloc Alloc/HEAP {Vec @ &self . capacity * item_size})}

        (Mem/copy &old_data {Vec @ &self . &data} {Vec @ &self . length * item_size})
    ))

    (fn Vec/reverse [(&self (& Vec))] [] (
        {start : u32 = 0}
        {end : u32 = {(Vec/len &self) - 1}}

        (loop (
            (if {start >= end} (break))

            (Vec/swap &self start end)
            {start += 1}
            {end -= 1}
        ))
    ))

    (fn Vec/as_slice [(&self (& Vec))] [ptr u32] (
        (return {Vec @ &self . &data} {Vec @ &self . length})
    ))

    (fn Vec/at [(&self (& Vec)) (index u32)] [u32] (
        (return {Vec @ &self . &data + {index * {Vec @ &self . item_size}}})
    ))

; string

    (struct String (actual (& Vec<u8>)))
    (struct StringSlice (data (&* u8)) (size u32))

    (fn StringSlice/char_at [(self StringSlice) (index u32)] [u8] (
        (return {i32/u8 @ {self . data} @ index})
    ))

    (fn StringSlice/clone [(self StringSlice)] [StringSlice] (
        {&cloned_data : (&* u8) = (Heap/alloc {self . size})}
        (Mem/copy {self . data} &cloned_data {self . size})
        (return (struct.new StringSlice &cloned_data {self . size}))
    ))

    (fn String/from_slice [(str StringSlice)] [(& String)] (
        {&string : (& String) = (Vec/new {str . size} 1)}
        (Vec/push_all &string str)
        (return &string)
    ))

; conversions

    (fn std/u32_to_string [(input u32)] [(& String)] (
        (return (std/u32_to_string_using input Alloc/HEAP))
    ))

    (fn std/u32_to_string_using [(input u32) (alloc_id u32)] [(& String)] (
        {&output : (& String) = (Vec/new_using 32 1 alloc_id)}

        (if {input == 0} (do
            (Vec/push_u8 &output (char_code "0"))
            (return &output)
        ))

        (loop (
            {rem : u32 = {input % 10}}

            (Vec/push_u8 &output {(char_code "0") + rem})
            {input = {input / 10}}

            (if {input == 0} (break))
        ))

        (Vec/reverse &output)

        (return &output)
    ))

    (fn std/c_str_to_slice [(&str (&* u8))] [StringSlice] (
        {len : u32 = 0}

        (loop (
            (if {{i32/u8 @ &str @ len} == 0} (break))

            {len += 1}
        ))

        (return (struct.new StringSlice &str len))
    ))
