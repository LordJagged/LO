(mod lib/cli)
(mod lib/wasi_io)
(mod lib/args)
(mod lib/lole/parser)

(fn main [] void (
    {(do {fd : u32} {file_name : StringSlice}) = (get_input_file)}
    (defer (Heap/free {file_name . data}))

    {&source := (WasiIO/fd_read_all fd)}
    (defer (Vec/free &source))

    {parse_res := (Lole/Parser/parse file_name (Vec/as_slice &source))}
    (if (not {parse_res . ok}) (do
        (return (print_err_and_exit {parse_res . data}))
    ))

    {&exprs : (& Vec<Lole/Ast/SExpr>) = {parse_res . data}}
    (defer (Vec<Lole/Ast/SExpr>/free &exprs))

    (dump_exprs &exprs)
    (puts "\n")
))

(fn dump_exprs [(&exprs (& Vec<Lole/Ast/SExpr>))] void (
    {i := 0}

    (loop (
        (if {i == (Vec/len &exprs)}
            (break)
        )

        (dump_expr {Lole/Ast/SExpr @ (Vec/at &exprs i)})

        (if {i != {(Vec/len &exprs) - 1}} (do
            (puts " ")
        ))

        {i += 1}
    ))
))

(fn dump_expr [(expr Lole/Ast/SExpr)] void (
    (if {expr . kind == Lole/Ast/SExpr/ATOM} (do
        {atom := {Lole/Ast/SExpr/Atom @ {expr . &value}}}
        (print_str {atom . &value})
        (return)
    ))

    (puts "(")

    {list := {Lole/Ast/SExpr/List @ {expr . &value}}}
    (dump_exprs {list . &value})

    (puts ")")
))

(fn get_input_file [] (tuple u32 StringSlice) (
    {args := (Args/load)}
    (defer (Args/free args))

    (if {args . argc != 2} (do
        (return Wasi/FD_STDIN (StringSlice/clone "<internal>"))
    ))

    {file_name : StringSlice = (StringSlice/clone (Args/get args 1))}
    {(do {err : u32} {fd : u32}) = (WasiIO/fd_open file_name)}
    (if {err != 0} (do
        (fputs Wasi/FD_STDERR "Error: cannot read file: ")
        (fputs Wasi/FD_STDERR file_name)
        (fputs Wasi/FD_STDERR "\n")
        (Wasi/proc_exit 1)
    ))

    (return fd file_name)
))

(fn print_err_and_exit [(&err (& Lole/Ast/CompileError))] void (
    {parse_err := (* &err)}
    (defer (Lole/Ast/CompileError/free parse_err))

    (fputs Wasi/FD_STDERR {parse_err . loc . file_name})
    (fputs Wasi/FD_STDERR ":")
    (fprint_u32 Wasi/FD_STDERR {parse_err . loc . line})
    (fputs Wasi/FD_STDERR ":")
    (fprint_u32 Wasi/FD_STDERR {parse_err . loc . col})
    (fputs Wasi/FD_STDERR " - ")
    (fprint_str Wasi/FD_STDERR {parse_err . &message})
    (fputs Wasi/FD_STDERR "\n")

    (return (Wasi/proc_exit 1))
))
