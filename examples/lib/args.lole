(struct Args
    (argc u32)
    (argv (&* (&* u8)))
    (argv_buf (&* u8))
)

(fn Args/load [] Args (
    {argc := (new u32 0 :using Alloc/STACK)}
    (defer (Stack/free (sizeof u32)))
    {argv_buf_size := (new u32 0 :using Alloc/STACK)}
    (defer (Stack/free (sizeof u32)))
    (drop (Wasi/args_sizes_get argc argv_buf_size))

    {argv_buf_size := (* argv_buf_size)}
    {argc := (* argc)}

    {argv := {(Heap/alloc {argc * (sizeof (&* u8))}) as (&* (&* u8))}}
    {argv_buf := {(Heap/alloc argv_buf_size) as (&* u8)}}
    (drop (Wasi/args_get argv argv_buf))

    (return (Args
        :argc argc
        :argv argv
        :argv_buf argv_buf
    ))
))

(fn Args/free [(self Args)] void (
    (Heap/free {self . argv})
    (Heap/free {self . argv_buf})
))

(fn Args/get [(self Args) (i u32)] StringSlice (
    {arg := {(&* u8) @ {self . argv + {i * (sizeof (&* u8))}}}}
    (return (std/c_str_to_slice arg))
))
